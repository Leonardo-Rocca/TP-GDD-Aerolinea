/*
USE[GD2C2015];
GO
*/

/* -- Nuevo esquema -- */

CREATE SCHEMA [DBAS] AUTHORIZATION [gd];
GO

/* -- Tablas y Constraints -- */

-- DDL de tablas

CREATE TABLE DBAS.rutas (
	codigo_ruta bigint IDENTITY(1,1) PRIMARY KEY,
	codigo_ruta_original bigint,
	ciudad_origen_id bigint,
	ciudad_destino_id bigint,
	precio_base_por_KG float(24),
	precio_base_por_pasaje float(24),
);
GO

CREATE TABLE DBAS.viajes (
	id_viaje bigint IDENTITY(1,1) PRIMARY KEY,
	fecha_salida datetime,
	fecha_llegada datetime,
	fecha_llegada_estimada datetime,
	matricula_aeronave varchar(100),
	codigo_ruta bigint,
	CHECK( (fecha_llegada_estimada <= DATEADD(day, 1, fecha_salida)) AND (fecha_salida < fecha_llegada))
);
GO

CREATE TABLE DBAS.aeronaves (
	matricula_aeronave varchar(100) PRIMARY KEY,
	numero_aeronave bigint IDENTITY(1,1) UNIQUE,
	modelo_aeronave varchar(100),
	baja_fuera_de_servicio tinyint default 0,
	baja_vida_util tinyint default 0,
	fecha_reinsercion datetime,
	fecha_baja_servicio_definitiva datetime,
	kg_disponible_encomienda float(24),
	id_fabricante bigint,
 	id_servicio bigint

);
GO

CREATE TABLE DBAS.fabricantes (
	id_fabricante bigint IDENTITY(1,1) PRIMARY KEY,
	nombre_fabricante varchar(50)
);
GO

CREATE TABLE DBAS.butacas (
	id_butaca bigint IDENTITY(1,1) PRIMARY KEY,
	numero_butaca smallint,
	tipo_butaca varchar(15) CHECK(tipo_butaca IN('Pasillo','Ventanilla','0')),
	piso_butaca tinyint,
	matricula_aeronave varchar(100) NOT NULL,
	UNIQUE(numero_butaca, matricula_aeronave, piso_butaca)
);
GO

CREATE TABLE DBAS.pasajes (
	codigo_pasaje bigint IDENTITY(1,1) PRIMARY KEY,
	id_cliente  bigint NOT NULL,
	id_viaje bigint NOT NULL,
	id_butaca bigint,
	fecha_compra_pasaje datetime NOT NULL,
	precio_pasaje float(24),
	id_compra_PNR bigint,
	id_cancelacion bigint
);
GO

CREATE TABLE DBAS.encomiendas (
	codigo_encomienda bigint IDENTITY(1,1) PRIMARY KEY,
	id_cliente bigint NOT NULL,
	encomienda_cliente_KG float(24),
	id_viaje bigint NOT NULL,
	fecha_compra_encomienda datetime NOT NULL,
	precio_encomienda float(24),
	id_compra_PNR bigint,
	id_cancelacion bigint
);

CREATE TABLE DBAS.servicios (
	id_servicio bigint IDENTITY(1,1) PRIMARY KEY,
	porcentaje_arancel float(24),
	tipo_servicio varchar(50)
);
GO

CREATE TABLE DBAS.serviciosPorRutas (
	codigo_ruta bigint NOT NULL,
	id_servicio bigint NOT NULL
);
GO

CREATE TABLE DBAS.compras (
	id_compra_PNR bigint IDENTITY(1,1) PRIMARY KEY,
	id_cliente bigint NOT NULL,
	tarjeta_id bigint
);
GO

CREATE TABLE DBAS.tarjetas (
	tarjeta_id bigint IDENTITY(1,1) PRIMARY KEY,
	tarjeta_Numero bigint,
	tarjeta_Codigo bigint,
	tarjeta_Fecha_de_vencimiento datetime,
	tipo_tarjeta_id bigint,
	cuoatas_elegidas smallint
	
);

CREATE TABLE DBAS.tiposTarjeta (
	tipo_tarjeta_id bigint IDENTITY(1,1) PRIMARY KEY,
	descripcion_tipo varchar(10),
	cuotas smallint
	
);

CREATE TABLE DBAS.roles (
	id_rol bigint IDENTITY(1,1) PRIMARY KEY,
	nombre_rol varchar(100),
	habilitado_rol tinyint default 1
);
GO

CREATE TABLE DBAS.rolesPorFuncionalidad (
	id_rol bigint NOT NULL,
	id_funcionalidad bigint NOT NULL,
);
GO

CREATE TABLE DBAS.funcionalidades (
	id_funcionalidad bigint IDENTITY(1,1) PRIMARY KEY,
	descripcion varchar(100)
);
GO

CREATE TABLE DBAS.cancelaciones (
	id_cancelacion bigint IDENTITY(1,1) PRIMARY KEY,
	fecha_devolucion datetime,
	motivo_cancelacion varchar(100),
);
GO

CREATE TABLE DBAS.usuarios (
	id_usuario bigint PRIMARY KEY,
	username varchar(50) UNIQUE,
	password varchar(250),
	intentos_fallidos tinyint default 0,
	habilitado tinyint default 1,
);
GO

CREATE TABLE DBAS.usuariosPorRol (
	id_usuario bigint NOT NULL,
	id_rol bigint NOT NULL
);
GO

CREATE TABLE DBAS.ciudades (
	id_ciudad bigint IDENTITY(1,1) PRIMARY KEY,
	nombre_ciudad varchar(100)
);
GO

CREATE TABLE DBAS.clientes (
	id_cliente bigint  IDENTITY(1,1) PRIMARY KEY,
	id_persona bigint NOT NULL,
);
GO

CREATE TABLE DBAS.personas (
	id_persona bigint IDENTITY(1,1) PRIMARY KEY,
	dni_persona bigint NOT NULL,
	nombre_persona varchar(100) NOT NULL,
	apellido_persona varchar(100) NOT NULL,
	direccion_persona varchar(100),
	telefono_persona varchar(50),
	mail_persona varchar(100),
	fecha_nacimiento datetime,
);
GO

CREATE TABLE DBAS.canjes (
	id_canje bigint IDENTITY(1,1) PRIMARY KEY,
	id_cliente bigint,
	fecha_canje datetime,
	cantidad_producto bigint,
	millas_usadas bigint,
	id_producto bigint
	
);
GO

CREATE TABLE DBAS.productos (
	id_producto bigint IDENTITY(1,1) PRIMARY KEY,
	valor_en_milas int,
	nombre_producto varchar(100),
	stock int
);
GO

CREATE TABLE DBAS.periodosFueraDeServicio (
	id_periodo bigint IDENTITY(1,1) PRIMARY KEY,
	matricula_aeronave varchar(100),
	fecha_fuera_servicio datetime,
	fecha_reinicio_servicio datetime
);
GO

--DDL de constraints compuestos

alter table DBAS.serviciosPorRutas add constraint pk_serviciosPorRutas
	primary key clustered (codigo_ruta,	id_servicio);
GO

alter table DBAS.rolesPorFuncionalidad add constraint pk_rolesPorFuncionalidad 
	primary key clustered (id_rol, id_funcionalidad);
GO

alter table DBAS.usuariosPorRol add constraint pk_usuariosPorRol
	primary key clustered (id_usuario, id_rol);
GO

-- DDL de constraints FK

alter table DBAS.rutas add constraint fk_ruta_ciudadOrigen 
	foreign key (ciudad_origen_id) references DBAS.ciudades (id_ciudad);
GO

alter table DBAS.rutas add constraint fk_ruta_ciudadDestino 
	foreign key (ciudad_destino_id) references DBAS.ciudades (id_ciudad);
GO

alter table DBAS.viajes add constraint fk_viaje_aeronave 
	foreign key (matricula_aeronave) references DBAS.aeronaves (matricula_aeronave);
GO	

alter table DBAS.viajes add constraint fk_viaje_ruta
	foreign key (codigo_ruta) references DBAS.rutas (codigo_ruta);
GO

alter table DBAS.aeronaves add constraint fk_aeronave_fabricante
	foreign key (id_fabricante) references DBAS.fabricantes (id_fabricante);
GO 
	
alter table DBAS.aeronaves add constraint fk_aeronave_servicio
	foreign key (id_servicio) references DBAS.servicios (id_servicio);
GO 

alter table DBAS.butacas add constraint fk_butaca_aeronave
	foreign key (matricula_aeronave) references DBAS.aeronaves (matricula_aeronave);
GO

alter table DBAS.pasajes add constraint fk_pasaje_cliente
	foreign key (id_cliente) references DBAS.clientes (id_cliente); 
GO

alter table DBAS.pasajes add constraint fk_pasaje_butaca
	foreign key (id_butaca) references DBAS.butacas (id_butaca);
GO

alter table DBAS.pasajes add constraint fk_pasaje_compra
	foreign key (id_compra_PNR) references DBAS.compras (id_compra_PNR);
GO

alter table DBAS.pasajes add constraint fk_pasaje_viaje
	foreign key (id_viaje) references DBAS.viajes (id_viaje);
GO

alter table DBAS.pasajes add constraint fk_pasaje_cancelacion
	foreign key (id_cancelacion) references DBAS.cancelaciones (id_cancelacion);
GO

alter table DBAS.encomiendas add constraint fk_encomienda_cliente
	foreign key (id_cliente) references DBAS.clientes (id_cliente); 
GO

alter table DBAS.encomiendas add constraint fk_encomienda_compra
	foreign key (id_compra_PNR) references DBAS.compras (id_compra_PNR);
GO

alter table DBAS.encomiendas add constraint fk_encomienda_viaje
	foreign key (id_viaje) references DBAS.viajes (id_viaje);
GO

alter table DBAS.encomiendas add constraint fk_encomienda_cancelacion
	foreign key (id_cancelacion) references DBAS.cancelaciones (id_cancelacion);
GO

alter table DBAS.compras add constraint fk_compra_cliente
	foreign key (id_cliente ) references DBAS.clientes (id_cliente);
GO

alter table DBAS.compras add constraint fk_compra_tarjeta
	foreign key (tarjeta_id) references DBAS.tarjetas (tarjeta_id);
GO

alter table DBAS.tarjetas add constraint fk_tarjeta_tipoTarjeta
	foreign key (tipo_tarjeta_id) references DBAS.tiposTarjeta (tipo_tarjeta_id);
GO

alter table DBAS.clientes add constraint fk_cliente_persona
	foreign key (id_persona) references DBAS.personas (id_persona);
GO

alter table DBAS.canjes add constraint fk_canje_cliente
	foreign key (id_cliente) references DBAS.clientes (id_cliente);
GO

alter table DBAS.canjes add constraint fk_canje_producto
	foreign key (id_producto) references DBAS.productos (id_producto);
GO

alter table DBAS.periodosFueraDeServicio add constraint fk_periodos_aeronave
	foreign key (matricula_aeronave) references DBAS.aeronaves (matricula_aeronave);	
GO

alter table DBAS.rolesPorFuncionalidad add constraint fk_rolesPorFuncionalidad_rol
	foreign key (id_rol) references DBAS.roles (id_rol);
GO

alter table DBAS.rolesPorFuncionalidad add constraint fk_rolesPorFuncionalidad_funcionalidad
	foreign key (id_funcionalidad) references DBAS.funcionalidades (id_funcionalidad);
GO

alter table DBAS.usuariosPorRol add constraint fk_usuariosPorRol_usuarios
	foreign key (id_usuario) references DBAS.usuarios (id_usuario);
GO

alter table DBAS.usuariosPorRol add constraint fk_usuariosPorRol_rol
	foreign key (id_rol) references DBAS.roles (id_rol);
GO

/* -- Migracion -- */

--Tablas temporales para la migracion

--Creo una tabla temporal que va a ser de ayuda para la migracion de los pasajes y asi utilizar menos tabla y ganar tiempo de busqueda de las ids
CREATE TABLE DBAS.#temporalViajesConRutas(id_viaj bigint, codigo_rut bigint, nom_ruta_orig varchar(100), nom_ruta_dest varchar(100), fecha_salida datetime, fecha_llegada datetime, matricula_aeronave varchar(100));
GO

--Creo una tabla temporal con el fin de utilizarla como ayuda para la migracion de los pasajes y asi buscar menos ids y mejorar el tiempo de migracion
CREATE TABLE DBAS.#temporalPasajesDeClientes(id_cliente bigint, fecha_llegada datetime, fecha_salida datetime, ciudad_origen varchar(100), ciudad_destino varchar(100), Pasaje_Codigo bigint, Pasaje_Precio float(24), Pasaje_FechaCompra datetime, Paquete_Codigo bigint, Paquete_Precio float(24), Paquete_FechaCompra datetime, Paquete_KG float(24), Butaca_Nro smallint, Butaca_Piso tinyint, Aeronave_Matricula varchar(100));
GO

--Creacion de triggers para la migracion

--Creo un trigger que sirve para asociar el id de un cliente con el de la persona y lo ordeno por su id para 
--asegurarme de que tanto el id del cliente como el de la persona sean identicos para asi utilizar 
--el id de la persona sin buscarlo en la tabla clientes para la migracion
CREATE TRIGGER DBAS.migrarClientesEnPersonaTrigger ON DBAS.personas
AFTER INSERT
AS
BEGIN
	INSERT INTO DBAS.clientes(id_persona)
	SELECT id_persona FROM inserted
	ORDER BY id_persona
END;
GO

--Creo un trigger en persona para asi una vez insertadas las personas utilizar sus id para la relacionar los pasajes de la maestra con el id del cliente que lo posee
CREATE TRIGGER DBAS.temporalPasajesDeClientestrigger ON DBAS.personas
AFTER INSERT
AS
BEGIN
	INSERT INTO #temporalPasajesDeClientes(id_cliente, fecha_llegada, fecha_salida, ciudad_origen, ciudad_destino, Pasaje_Codigo, Pasaje_Precio, Pasaje_FechaCompra, Paquete_Codigo, Paquete_Precio, Paquete_FechaCompra, Paquete_KG, Butaca_Nro, Butaca_Piso, Aeronave_Matricula)
	SELECT tablaPersona.id_persona, tablaMaestra.FechaLLegada, tablaMaestra.FechaSalida,  SUBSTRING(tablaMaestra.Ruta_Ciudad_Origen,2,LEN(tablaMaestra.Ruta_Ciudad_Origen)-1) , SUBSTRING(tablaMaestra.Ruta_Ciudad_Destino,2,LEN(tablaMaestra.Ruta_Ciudad_Destino)-1), tablaMaestra.Pasaje_Codigo, tablaMaestra.Pasaje_Precio, tablaMaestra.Pasaje_FechaCompra, tablaMaestra.Paquete_Codigo, tablaMaestra.Paquete_Precio, tablaMaestra.Paquete_FechaCompra, tablaMaestra.Paquete_KG, tablaMaestra.Butaca_Nro, tablaMaestra.Butaca_Piso, tablaMaestra.Aeronave_Matricula 
	FROM ((SELECT id_persona, dni_persona, nombre_persona, apellido_persona FROM inserted) tablaPersona
	JOIN (SELECT Cli_Dni, Cli_Apellido, Cli_Nombre, Pasaje_Codigo, Pasaje_Precio, Pasaje_FechaCompra, Paquete_Codigo, Paquete_Precio, Paquete_FechaCompra, Paquete_KG, Butaca_Nro, Butaca_Piso, FechaSalida, FechaLLegada, Ruta_Ciudad_Origen, Ruta_Ciudad_Destino, Aeronave_Matricula FROM gd_esquema.Maestra) tablaMaestra
	ON (tablaPersona.dni_persona = tablaMaestra.Cli_Dni AND tablaPersona.nombre_persona = tablaMaestra.Cli_Nombre AND tablaPersona.apellido_persona = tablaMaestra.Cli_Apellido))
END;
GO

--Creo un trigger para la insercion de los valores en la tabla temporal una vez que se insertaron los viajes
CREATE TRIGGER DBAS.migrarDatosATemporalTrigger ON DBAS.viajes
AFTER INSERT
AS
BEGIN
	
	INSERT INTO #temporalViajesConRutas(id_viaj, codigo_rut, nom_ruta_orig, nom_ruta_dest, fecha_salida, fecha_llegada, matricula_aeronave)
	SELECT tablaViajes.id_viaje, tablaRutas.codigo_ruta, tablaCiudadOrig.nombre_ciudad, tablaCiudadDest.nombre_ciudad, tablaViajes.fecha_salida, tablaViajes.fecha_llegada, tablaViajes.matricula_aeronave 
	FROM ((SELECT id_viaje, fecha_salida, fecha_llegada, codigo_ruta, matricula_aeronave FROM inserted) tablaViajes
		JOIN (SELECT codigo_ruta, ciudad_origen_id, ciudad_destino_id FROM DBAS.rutas) tablaRutas
		ON (tablaViajes.codigo_ruta = tablaRutas.codigo_ruta)
			JOIN DBAS.ciudades tablaCiudadOrig ON (tablaRutas.ciudad_origen_id = tablaCiudadOrig.id_ciudad)
			JOIN DBAS.ciudades tablaCiudadDest ON (tablaRutas.ciudad_destino_id = tablaCiudadDest.id_ciudad))
END;
GO

--Creo stored procedures para la migracion

CREATE PROCEDURE DBAS.migracionFabricantes
AS
BEGIN

	--Se migran los fabricante de la tabla Maestra
	INSERT INTO DBAS.fabricantes(nombre_fabricante)
	SELECT DISTINCT Aeronave_Fabricante 
	FROM gd_esquema.Maestra

END;
GO

CREATE PROCEDURE DBAS.migracionCiudades
AS
BEGIN
	
	--Se migran las ciudades destino y origen quedando solo las que no se repiten
	INSERT INTO DBAS.ciudades(nombre_ciudad)
	SELECT DISTINCT SUBSTRING(Ruta_Ciudad_Origen,2,LEN(Ruta_Ciudad_Origen)-1) FROM gd_esquema.Maestra UNION SELECT DISTINCT SUBSTRING(Ruta_Ciudad_Destino,2,LEN(Ruta_Ciudad_Origen)-1) FROM gd_esquema.Maestra

END;
GO

CREATE PROCEDURE DBAS.migracionServicios
AS
BEGIN
	--Se migran los servivios de la tabla maestra
	INSERT INTO DBAS.servicios(tipo_servicio, porcentaje_arancel)
	SELECT Tipo_Servicio, ROUND(SUM(Pasaje_Precio/Ruta_Precio_BasePasaje)/COUNT(Pasaje_Codigo),2) FROM gd_esquema.Maestra 
	WHERE Pasaje_Codigo != 0
	GROUP BY Tipo_Servicio
END;
GO

CREATE PROCEDURE DBAS.migracionRutas
AS
BEGIN
	DECLARE @id_ciudad_original bigint
	DECLARE @id_ciudad_destino bigint
	DECLARE @precio_KG float(24)
	DECLARE @precio_pasaje float(24)
	DECLARE @id_servicio bigint
	DECLARE @id_ruta bigint
	
	DECLARE migrarRutasConServicios CURSOR FOR
	SELECT DISTINCT tablaCiudades_orig.id_ciudad, tablaCiudades_dest.id_ciudad, tablaAux2.Ruta_Precio_BaseKG, tablaAux1.Ruta_Precio_BasePasaje, tablaServicios.id_servicio
			FROM ((SELECT DISTINCT Ruta_Codigo, Ruta_Ciudad_Origen, Ruta_Ciudad_Destino,Ruta_Precio_BasePasaje, Tipo_Servicio FROM gd_esquema.Maestra WHERE Ruta_Precio_BasePasaje > 0.00) tablaAux1
				JOIN (SELECT DISTINCT Ruta_Codigo,Ruta_Ciudad_Origen, Ruta_Ciudad_Destino,Ruta_Precio_BaseKG, Tipo_Servicio FROM gd_esquema.Maestra WHERE Ruta_Precio_BASEKG > 0.00) tablaAux2
					ON (tablaAux1.Ruta_Ciudad_Origen = tablaAux2.Ruta_Ciudad_Origen AND tablaAux1.Ruta_Ciudad_Destino = tablaAux2.Ruta_Ciudad_Destino)
					JOIN DBAS.ciudades tablaCiudades_dest ON ( SUBSTRING(tablaAux1.Ruta_Ciudad_Destino,2,LEN(tablaAux1.Ruta_Ciudad_Destino)-1) = tablaCiudades_dest.nombre_ciudad)
					JOIN DBAS.ciudades  tablaCiudades_orig ON ( SUBSTRING(tablaAux1.Ruta_Ciudad_Origen,2,LEN(tablaAux1.Ruta_Ciudad_Origen)-1) = tablaCiudades_orig.nombre_ciudad)
					JOIN DBAS.servicios tablaServicios ON (tablaAux1.Tipo_Servicio = tablaServicios.tipo_servicio))

	OPEN migrarRutasConServicios

	-- Inserto lo que obtiene el cursor en las variables
	FETCH NEXT FROM migrarRutasConServicios
	INTO @id_ciudad_original, @id_ciudad_destino, @precio_KG, @precio_pasaje, @id_servicio

	WHILE @@FETCH_STATUS = 0
	BEGIN
		
		INSERT INTO DBAS.rutas(ciudad_origen_id, ciudad_destino_id, precio_base_por_KG, precio_base_por_pasaje)
		VALUES(@id_ciudad_original, @id_ciudad_destino, @precio_KG, @precio_pasaje)

		SET @id_ruta = SCOPE_IDENTITY()

		INSERT INTO DBAS.serviciosPorRutas(codigo_ruta, id_servicio)
		VALUES(@id_ruta, @id_servicio)

		-- Inserto lo que obtiene el cursor en las variables
		FETCH NEXT FROM migrarRutasConServicios
		INTO @id_ciudad_original, @id_ciudad_destino, @precio_KG, @precio_pasaje, @id_servicio

	END 
	
	CLOSE migrarRutasConServicios
	DEALLOCATE migrarRutasConServicios

END;
GO

/*
-- sirve para saber si hay consistencia con el tipo de servicio de la aeronave
SELECT DISTINCT aux1.Aeronave_matricula, aux1.Aeronave_KG_Disponibles, aux1.Aeronave_Fabricante, aux1.Tipo_Servicio 
FROM ((SELECT Aeronave_matricula, Aeronave_KG_Disponibles, Aeronave_Fabricante, Tipo_Servicio FROM gd_esquema.Maestra) aux1 JOIN (SELECT Aeronave_matricula, Aeronave_KG_Disponibles, Aeronave_Fabricante, Tipo_Servicio FROM gd_esquema.Maestra) aux2 
ON (aux1.Aeronave_Matricula = aux2.Aeronave_Matricula AND aux1.Tipo_Servicio != aux2.Tipo_Servicio)) 

-- sirve para saber si hay consistencia con kg disponibles
SELECT DISTINCT aux1.Aeronave_matricula, aux1.Aeronave_KG_Disponibles, aux1.Aeronave_Fabricante, aux1.Tipo_Servicio 
FROM ((SELECT Aeronave_matricula, Aeronave_KG_Disponibles, Aeronave_Fabricante, Tipo_Servicio FROM gd_esquema.Maestra) aux1 JOIN (SELECT Aeronave_matricula, Aeronave_KG_Disponibles, Aeronave_Fabricante, Tipo_Servicio FROM gd_esquema.Maestra) aux2 
ON (aux1.Aeronave_Matricula = aux2.Aeronave_Matricula AND aux1.Aeronave_KG_Disponibles != aux2.Aeronave_KG_Disponibles)) 
*/

CREATE PROCEDURE DBAS.migracionAeronaves
AS
BEGIN

	--Migro las aeronaves de la tabla Maestra, utilizando las tablas fabricantes y servicios para obtener sus ids
	INSERT INTO DBAS.aeronaves(matricula_aeronave, modelo_aeronave, kg_disponible_encomienda, id_fabricante, id_servicio)
	SELECT tablaAux.Aeronave_Matricula, tablaAux.Aeronave_Modelo, tablaAux.Aeronave_KG_Disponibles, tablaFab.id_fabricante, tablaServ.id_servicio
	FROM ((SELECT DISTINCT Aeronave_matricula, Aeronave_KG_Disponibles, Aeronave_Fabricante, Aeronave_Modelo, Tipo_Servicio FROM gd_esquema.Maestra) tablaAux
		JOIN DBAS.fabricantes tablaFab ON (tablaFab.nombre_fabricante = tablaAux.Aeronave_Fabricante)
			JOIN DBAS.servicios tablaServ ON (tablaServ.tipo_servicio = tablaAux.Tipo_Servicio))
END;
GO

CREATE PROCEDURE DBAS.migracionViajes
AS
BEGIN

	--Inserto la cantidad de viajes de la tabla maestra
	INSERT INTO DBAS.viajes (fecha_salida,fecha_llegada_estimada,fecha_llegada,matricula_aeronave,codigo_ruta)
	SELECT tablaAux.FechaSalida, tablaAux.Fecha_LLegada_Estimada, tablaAux.FechaLLegada, tablaAux.Aeronave_Matricula, tablaRutas.codigo_ruta
	FROM ((SELECT DISTINCT FechaSalida, Fecha_LLegada_Estimada, FechaLLegada, Aeronave_Matricula, Ruta_Ciudad_Origen, Ruta_Ciudad_Destino FROM gd_esquema.Maestra) tablaAux 
		JOIN DBAS.ciudades tablaciudad_orig ON (tablaciudad_orig.nombre_ciudad= SUBSTRING(tablaAux.Ruta_Ciudad_Origen,2,LEN(tablaAux.Ruta_Ciudad_Origen)-1) )
			JOIN DBAS.ciudades tablaciudad_dest ON (tablaciudad_dest.nombre_ciudad =   SUBSTRING(tablaAux.Ruta_Ciudad_Destino,2,LEN(tablaAux.Ruta_Ciudad_Destino)-1) )
				JOIN DBAS.rutas tablaRutas ON (tablaRutas.ciudad_origen_id = tablaciudad_orig.id_ciudad AND tablaRutas.ciudad_destino_id = tablaciudad_dest.id_ciudad))
END;
GO

CREATE PROCEDURE DBAS.migracionButacas
AS
BEGIN

	--Inserto las butacas de cada avion de la tabla maestra
	INSERT INTO DBAS.butacas (numero_butaca, tipo_butaca, piso_butaca, matricula_aeronave)
	SELECT DISTINCT Butaca_Nro, Butaca_Tipo, Butaca_Piso, Aeronave_Matricula FROM gd_esquema.Maestra

END;
GO

CREATE PROCEDURE DBAS.migracionClientes
AS
BEGIN

	--Migro las personas de la tabla maestra
	INSERT INTO DBAS.personas(dni_persona, nombre_persona, apellido_persona, direccion_persona, telefono_persona, mail_persona, fecha_nacimiento)
	SELECT DISTINCT Cli_Dni, Cli_Nombre, Cli_Apellido, Cli_Dir, Cli_Telefono, Cli_Mail, Cli_Fecha_Nac FROM gd_esquema.Maestra

END;
GO

CREATE PROCEDURE DBAS.migracionPasajes
AS
BEGIN

	--Activo la insercion en la variable identity de la tabla
	SET IDENTITY_INSERT DBAS.pasajes ON

	--Inserto los valores de las las tablas temporales que solo tengan pasajes y no encomiendas
	INSERT INTO DBAS.pasajes(codigo_pasaje, id_cliente, precio_pasaje, fecha_compra_pasaje, id_viaje, id_butaca)
	SELECT DISTINCT tablaTemporalPasajes.Pasaje_Codigo, tablaTemporalPasajes.id_cliente, tablaTemporalPasajes.Pasaje_Precio, tablaTemporalPasajes.Paquete_FechaCompra, tablaTemporalViajes.id_viaj, tablaButacas.id_butaca 
	FROM ((SELECT id_cliente, fecha_llegada, fecha_salida, ciudad_origen, ciudad_destino, Pasaje_Codigo, Pasaje_Precio, Pasaje_FechaCompra, Paquete_Codigo, Paquete_Precio, Paquete_FechaCompra, Paquete_KG, Butaca_Nro, Butaca_Piso, Aeronave_Matricula FROM #temporalPasajesDeClientes WHERE Butaca_Piso != 0) tablaTemporalPasajes
		JOIN DBAS.butacas tablaButacas ON (tablaTemporalPasajes.Aeronave_Matricula = tablaButacas.matricula_aeronave AND tablaTemporalPasajes.Butaca_Nro = tablaButacas.numero_butaca AND tablaTemporalPasajes.Butaca_Piso = tablaButacas.piso_butaca)
		JOIN #temporalViajesConRutas tablaTemporalViajes ON (tablaTemporalViajes.nom_ruta_orig = tablaTemporalPasajes.ciudad_origen AND tablaTemporalViajes.nom_ruta_dest = tablaTemporalPasajes.ciudad_destino AND tablaTemporalViajes.fecha_salida = tablaTemporalPasajes.fecha_salida AND tablaTemporalViajes.fecha_llegada = tablaTemporalPasajes.fecha_llegada AND tablaTemporalPasajes.Aeronave_Matricula = tablaTemporalViajes.matricula_aeronave))

	--Activo el identity para que se genere automaticamente nuevamente
	SET IDENTITY_INSERT DBAS.pasajes OFF

END;
GO

CREATE PROCEDURE DBAS.migracionEncomiendas
AS
BEGIN

	--Activo la insercion en la variable identity de la tabla
	SET IDENTITY_INSERT DBAS.encomiendas ON

	--Inserto los valores de las las tablas temporales que solo tengan encomiendas y no pasajes
	INSERT INTO DBAS.encomiendas(codigo_encomienda, id_cliente, encomienda_cliente_KG, precio_encomienda, fecha_compra_encomienda, id_viaje)
	SELECT DISTINCT tablaTemporalPasajes.Paquete_Codigo, tablaTemporalPasajes.id_cliente, tablaTemporalPasajes.Paquete_KG, tablaTemporalPasajes.Paquete_Precio, tablaTemporalPasajes.Paquete_FechaCompra, tablaTemporalViajes.id_viaj
	FROM ((SELECT id_cliente, fecha_llegada, fecha_salida, ciudad_origen, ciudad_destino, Pasaje_Codigo, Pasaje_Precio, Pasaje_FechaCompra, Paquete_Codigo, Paquete_Precio, Paquete_FechaCompra, Paquete_KG, Butaca_Nro, Butaca_Piso, Aeronave_Matricula FROM #temporalPasajesDeClientes WHERE Butaca_Piso = 0) tablaTemporalPasajes
		JOIN #temporalViajesConRutas tablaTemporalViajes ON (tablaTemporalViajes.nom_ruta_orig = tablaTemporalPasajes.ciudad_origen AND tablaTemporalViajes.nom_ruta_dest = tablaTemporalPasajes.ciudad_destino AND tablaTemporalViajes.fecha_salida = tablaTemporalPasajes.fecha_salida AND tablaTemporalViajes.fecha_llegada = tablaTemporalPasajes.fecha_llegada AND tablaTemporalPasajes.Aeronave_Matricula = tablaTemporalViajes.matricula_aeronave))


	--Activo el identity para que se genere automaticamente nuevamente
	SET IDENTITY_INSERT DBAS.encomiendas OFF

END;
GO

--Ejecuto los stored procedures
EXECUTE DBAS.migracionFabricantes
EXECUTE DBAS.migracionCiudades
EXECUTE DBAS.migracionServicios
EXECUTE DBAS.migracionRutas
EXECUTE DBAS.migracionAeronaves
EXECUTE DBAS.migracionViajes
EXECUTE DBAS.migracionButacas
EXECUTE DBAS.migracionClientes
EXECUTE DBAS.migracionPasajes
EXECUTE DBAS.migracionEncomiendas
GO

--Elimino las tablas temporales de la migracion

DROP TABLE #temporalPasajesDeClientes
GO

DROP TABLE #temporalViajesConRutas
GO

--Elimino los triggers de la migracion

DROP TRIGGER DBAS.temporalPasajesDeClientestrigger
GO

DROP TRIGGER DBAS.migrarDatosATemporalTrigger
GO

DROP TRIGGER DBAS.migrarClientesEnPersonaTrigger
GO

/* Roles y funcionalidades */

-- Se agregan las funcionalidades
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('ABM de Rol');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Registro de Usuario');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('ABM de Ciudades');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('ABM de Ruta Aerea');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('ABM de Aeronaves');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Generar Viaje'); 
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Registro de llegada Destino');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Compra de Pasaje/Encomienda');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Cancelación/Devolución de pasaje y/o encomienda');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Consulta de millas de pasajero frecuente');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Canje de millas de pasajero frecuente');
INSERT INTO DBAS.funcionalidades (descripcion) VALUES ('Listado Estadístico');
GO

-- insercion del rol administrador
INSERT INTO DBAS.roles(nombre_rol) VALUES('Administrador General');
GO

-- Se agregan funcionalidades al rol administrador
INSERT INTO DBAS.rolesPorFuncionalidad(id_rol, id_funcionalidad)
select tablaRol.id_rol, tablaFuncionalidad.id_funcionalidad from DBAS.roles  tablaRol, DBAS.funcionalidades tablaFuncionalidad
where tablaRol.nombre_rol = 'Administrador General'
GO

-- Se agregan usuarios administradores
INSERT INTO DBAS.personas(dni_persona,apellido_persona,nombre_persona)
VALUES(99999999,'Admin','Admin')

DECLARE @id_p bigint
SET @id_p = SCOPE_IDENTITY()

-- Se inserta el usuario admin, password w23e
INSERT INTO DBAS.usuarios(id_usuario,username,password)
VALUES (@id_p,'admin','e6b87050bfcb8143fcb8db0170a4dc9ed00d904ddd3e2a4ad1b1e8dc0fdc9be7')
GO

-- Se le asigna el rol administrador general al usuario admin
INSERT INTO DBAS.usuariosPorRol(id_usuario, id_rol)
SELECT tablaUsuarios.id_usuario, tablaRoles.id_rol FROM DBAS.usuarios tablaUsuarios, DBAS.roles tablaRoles
WHERE tablaRoles.nombre_rol = 'Administrador General' and tablaUsuarios.username = 'admin'
GO

/*-- TOP 5 -- */

CREATE FUNCTION DBAS.Top5DeLosDestinosConMasPasajesComprados(@semestreDelAnio tinyint, @anio integer)
RETURNS @TOP5 TABLE ( ciudad_origen varchar(100), ciudad_destino varchar(100), pasajes_comprados bigint)
AS
BEGIN

	DECLARE @anioDelSemestre datetime
	SET @anioDelSemestre = convert(datetime, concat(@anio, '0701 00:00:00.000'))

	IF (@semestreDelAnio = 1)
	BEGIN

		INSERT INTO @TOP5(ciudad_origen, ciudad_destino, pasajes_comprados)
		SELECT TOP 5 tablaCiudades1.nombre_ciudad, tablaCiudades2.nombre_ciudad, COUNT(tablaPasajes.codigo_pasaje) as pasajes_comprados FROM DBAS.pasajes tablaPasajes, DBAS.viajes tablaViajes, DBAS.rutas tablaRutas, DBAS.ciudades tablaCiudades1, DBAS.ciudades tablaCiudades2
		WHERE tablaPasajes.id_viaje = tablaViajes.id_viaje AND tablaViajes.codigo_ruta = tablaRutas.codigo_ruta AND tablaCiudades1.id_ciudad = tablaRutas.ciudad_origen_id AND tablaCiudades2.id_ciudad = tablaRutas.ciudad_destino_id
			AND tablaPasajes.fecha_compra_pasaje <  @anioDelSemestre AND DATEPART(year,tablaPasajes.fecha_compra_pasaje) = DATEPART(year,@anioDelSemestre)
		GROUP BY tablaCiudades1.nombre_ciudad, tablaCiudades2.nombre_ciudad
		ORDER BY pasajes_comprados DESC

		
	END
	ELSE
		BEGIN

			INSERT INTO @TOP5(ciudad_origen, ciudad_destino, pasajes_comprados)
			SELECT TOP 5 tablaCiudades1.nombre_ciudad, tablaCiudades2.nombre_ciudad, COUNT(tablaPasajes.codigo_pasaje) as pasajes_comprados FROM DBAS.pasajes tablaPasajes, DBAS.viajes tablaViajes, DBAS.rutas tablaRutas, DBAS.ciudades tablaCiudades1, DBAS.ciudades tablaCiudades2
			WHERE tablaPasajes.id_viaje = tablaViajes.id_viaje AND tablaViajes.codigo_ruta = tablaRutas.codigo_ruta AND tablaCiudades1.id_ciudad = tablaRutas.ciudad_origen_id AND tablaCiudades2.id_ciudad = tablaRutas.ciudad_destino_id
			AND tablaPasajes.fecha_compra_pasaje >=  @anioDelSemestre AND DATEPART(year,tablaPasajes.fecha_compra_pasaje) = DATEPART(year,@anioDelSemestre)
			GROUP BY tablaCiudades1.nombre_ciudad, tablaCiudades2.nombre_ciudad
			ORDER BY pasajes_comprados DESC

		END

	RETURN
END;	
GO


CREATE FUNCTION DBAS.aeronaves_dias_fuera_de_servicio(@semestreDelAnio tinyint, @anio integer)
RETURNS @aeronaves_dias_fuera_de_servicio TABLE ( aeronave varchar(100), dias_fuera_de_servicio bigint)
AS
BEGIN

	DECLARE @anioDelSemestre datetime
	SET @anioDelSemestre = convert(datetime, concat(@anio, '0701 00:00:00.000'))

	IF (@semestreDelAnio = 1)
	BEGIN
		
		INSERT INTO @aeronaves_dias_fuera_de_servicio(aeronave, dias_fuera_de_servicio)
		(SELECT tablaAeronaves.matricula_aeronave, SUM(DATEDIFF(day,tablaPeriodos.fecha_fuera_servicio,tablaPeriodos.fecha_reinicio_servicio)) as cantidad_dias_fuera_de_servicio 
		FROM DBAS.periodosFueraDeServicio tablaPeriodos, DBAS.aeronaves tablaAeronaves
		WHERE tablaPeriodos.fecha_reinicio_servicio IS NOT null AND tablaPeriodos.fecha_reinicio_servicio < @anioDelSemestre AND DATEPART(year,tablaPeriodos.fecha_reinicio_servicio) = DATEPART(year,@anioDelSemestre)
		GROUP BY tablaAeronaves.matricula_aeronave) 
		UNION ALL
		(SELECT tablaAeronaves.matricula_aeronave, SUM(DATEDIFF(day,tablaPeriodos.fecha_fuera_servicio,GETDATE())) as cantidad_dias_fuera_de_servicio 
		FROM DBAS.periodosFueraDeServicio tablaPeriodos, DBAS.aeronaves tablaAeronaves
		WHERE tablaPeriodos.fecha_reinicio_servicio IS null AND GETDATE() < @anioDelSemestre AND DATEPART(year,GETDATE()) = DATEPART(year,@anioDelSemestre)
		GROUP BY tablaAeronaves.matricula_aeronave)
				
	END
	ELSE
	BEGIN

		INSERT INTO @aeronaves_dias_fuera_de_servicio(aeronave, dias_fuera_de_servicio)
		(SELECT tablaAeronaves.matricula_aeronave, SUM(DATEDIFF(day,tablaPeriodos.fecha_fuera_servicio,tablaPeriodos.fecha_reinicio_servicio)) as cantidad_dias_fuera_de_servicio 
		FROM DBAS.periodosFueraDeServicio tablaPeriodos, DBAS.aeronaves tablaAeronaves
		WHERE tablaPeriodos.fecha_reinicio_servicio IS NOT null AND tablaPeriodos.fecha_reinicio_servicio >= @anioDelSemestre AND DATEPART(year,tablaPeriodos.fecha_reinicio_servicio) = DATEPART(year,@anioDelSemestre)
		GROUP BY tablaAeronaves.matricula_aeronave) 
		UNION ALL
		(SELECT tablaAeronaves.matricula_aeronave, SUM(DATEDIFF(day,tablaPeriodos.fecha_fuera_servicio,GETDATE())) as cantidad_dias_fuera_de_servicio 
		FROM DBAS.periodosFueraDeServicio tablaPeriodos, DBAS.aeronaves tablaAeronaves
		WHERE tablaPeriodos.fecha_reinicio_servicio IS null AND GETDATE() >= @anioDelSemestre AND DATEPART(year,GETDATE()) = DATEPART(year,@anioDelSemestre)
		GROUP BY tablaAeronaves.matricula_aeronave)

	END

	RETURN
END;	
GO

CREATE FUNCTION DBAS.Top5AeronavesConMayorCantidadDeDiasFueraDeServicio(@semestreDelAnio tinyint, @anio integer)
RETURNS @TOP5 TABLE ( aeronave varchar(100), dias_fuera_de_servicio bigint)
AS
BEGIN
	INSERT INTO @TOP5(aeronave, dias_fuera_de_servicio)
	SELECT TOP 5 tablaDias.aeronave,SUM(tablaDias.dias_fuera_de_servicio)
	FROM DBAS.aeronaves_dias_fuera_de_servicio(@semestreDelAnio, @anio) tablaDias
	GROUP BY tablaDias.aeronave
	ORDER BY tablaDias.dias_fuera_de_servicio DESC
				
	RETURN
END;	
GO


/*-- ABM Rol -- */

CREATE FUNCTION DBAS.obtenerFuncionalidadesAsociadas(@nombreRol varchar(100))
RETURNS TABLE
AS
RETURN
(
	SELECT tablaFuncionalidades.descripcion FROM ((SELECT * FROM DBAS.roles WHERE nombre_rol = @nombreRol) unRol 
		JOIN DBAS.rolesPorFuncionalidad tablaRpF ON (unRol.id_rol = tablaRpF.id_rol)
		JOIN DBAS.funcionalidades tablaFuncionalidades ON (tablaFuncionalidades.id_funcionalidad = tablaRpF.id_funcionalidad))
);
GO

/*
SELECT * FROM DBAS.obtenerFuncionalidadesAsociadas('Administrador General')
GO
*/

CREATE PROCEDURE DBAS.loginUsuario(@usuario varchar(100), @pass varchar(100))
AS
BEGIN
	DECLARE @U_id bigint
	DECLARE @U_password varchar(100)
	DECLARE @U_habilitado tinyint
	DECLARE @U_rol_id bigint
	DECLARE @cantidad_intentos tinyint
	DECLARE @mensaje_error varchar(100)
	DECLARE @nombre_rol varchar(100)

	SELECT @U_id = tablaUsuarios.id_usuario, @U_password = password, @U_habilitado = habilitado, @cantidad_intentos = intentos_fallidos, @U_rol_id = tablaUporR.id_rol FROM DBAS.usuarios tablaUsuarios, DBAS.usuariosPorRol tablaUporR WHERE tablaUsuarios.username = @usuario AND tablaUporR.id_usuario = tablaUsuarios.id_usuario
	IF (@U_id IS NOT NULL)
	BEGIN
		IF(@U_habilitado != 1)
		BEGIN
			SET @mensaje_error = 'El usuario ingresado se encuentra deshabilitado'
			RAISERROR(@mensaje_error, 12, 1)
		END
		ELSE
		BEGIN
			IF(@U_password != @pass)
			BEGIN
				IF(@cantidad_intentos < 2)
				BEGIN
					UPDATE DBAS.usuarios SET intentos_fallidos = @cantidad_intentos + 1  WHERE username = @usuario
					SET @mensaje_error = 'Password invalida'
					RAISERROR(@mensaje_error, 12, 1)
				END
				ELSE
				BEGIN
					UPDATE DBAS.usuarios SET intentos_fallidos = @cantidad_intentos + 1, habilitado = 0   WHERE username = @usuario
					SET @mensaje_error = 'Password invalida, alcanzo la cantidad de intentos maximos. Se desactivara la cuenta por seguridad'
					RAISERROR(@mensaje_error, 12, 1)
				END
			END
			ELSE
			BEGIN
				UPDATE DBAS.usuarios SET intentos_fallidos = 0  WHERE username = @usuario

				SELECT @nombre_rol = nombre_rol FROM DBAS.roles WHERE id_rol = @U_rol_id
				SELECT * FROM DBAS.obtenerFuncionalidadesAsociadas(@nombre_rol)
			END
		END
	END
	ELSE
	BEGIN
		SET @mensaje_error = 'El usuario ingresado no existe'
		RAISERROR(@mensaje_error, 12, 1)
	END
END;
GO


CREATE PROCEDURE DBAS.altaRol(@nombre_rol varchar(100), @nombre_funcionalidades varchar(100))
AS
BEGIN
	DECLARE @mensaje_error varchar(100)
	DECLARE @rol_id bigint

	INSERT INTO DBAS.roles (nombre_rol) VALUES (@nombre_rol)

	SET @rol_id = SCOPE_IDENTITY()

	INSERT INTO DBAS.rolesPorFuncionalidad(id_rol, id_funcionalidad)
	SELECT @rol_id, id_funcionalidad FROM DBAS.funcionalidades
	WHERE descripcion = @nombre_funcionalidades

END;
GO

CREATE PROCEDURE DBAS.modificarRol(@id_rol bigint, @nombre_rol_nuevo varchar(100), @id_funcionalidad bigint, @habilitado tinyint)
AS
BEGIN
	DECLARE @habilitado_estado tinyint
	DECLARE @mensaje_error varchar(100)

	SELECT @habilitado_estado = habilitado_rol FROM DBAS.roles

	IF(@habilitado_estado = 1 AND @habilitado = 0)
	BEGIN
		DELETE FROM DBAS.usuariosPorRol WHERE id_rol = @id_rol
	END

	UPDATE DBAS.roles SET nombre_rol = @nombre_rol_nuevo, habilitado_rol = @habilitado WHERE id_rol = @id_rol

	DELETE FROM DBAS.rolesPorFuncionalidad
	WHERE id_rol = @id_rol

	INSERT INTO DBAS.rolesPorFuncionalidad(id_rol, id_funcionalidad)
	VALUES(@id_rol, @id_funcionalidad)

END;
GO


CREATE VIEW DBAS.rolesConFuncionalidades AS
SELECT tablaRoles.nombre_rol, tablaRporF.id_funcionalidad FROM DBAS.rolesPorFuncionalidad tablaRporF, DBAS.roles tablaRoles 
WHERE tablaRporF.id_rol = tablaRoles.id_rol
GO

CREATE TRIGGER DBAS.crearRolConFuncionalidades ON DBAS.rolesConFuncionalidades
INSTEAD OF INSERT
AS
BEGIN

	DECLARE @nombre_rol varchar(100)
	DECLARE @id_funcionalidad bigint
	DECLARE @id_rol bigint
	DECLARE @valor_insertado tinyint

	SET @valor_insertado = 0
	
	DECLARE obtenerRolYFuncionalidades CURSOR FOR
	SELECT nombre_rol, id_funcionalidad FROM inserted

	OPEN obtenerRolYFuncionalidades

	-- Inserto lo que obtiene el cursor en las variables
	FETCH NEXT FROM obtenerRolYFuncionalidades
	INTO @nombre_rol, @id_funcionalidad

	WHILE @@FETCH_STATUS = 0
	BEGIN
		IF(@valor_insertado = 0)
		BEGIN
			INSERT INTO DBAS.roles(nombre_rol)
			VALUES(@nombre_rol)

			SET @id_rol = SCOPE_IDENTITY()
			SET @valor_insertado =1
		END

		INSERT INTO rolesPorFuncionalidad(id_rol, id_funcionalidad)
		VALUES(@id_rol, @id_funcionalidad)

		-- Inserto lo que obtiene el cursor en las variables
		FETCH NEXT FROM obtenerRolYFuncionalidades
		INTO @nombre_rol, @id_funcionalidad

	END

	CLOSE obtenerRolYFuncionalidades
	DEALLOCATE obtenerRolYFuncionalidades

END;
GO

CREATE PROCEDURE DBAS.altaPersona(
	@dni_persona bigint ,
	@nombre_persona varchar(100),
	@apellido_persona varchar(100),
	@direccion_persona varchar(100),
	@telefono_persona varchar(50),
	@mail_persona varchar(100),
	@fecha_nacimiento datetime,
	@ID_persona bigint OUT)
AS
BEGIN
	
	INSERT INTO DBAS.personas(dni_persona,nombre_persona,apellido_persona,direccion_persona,telefono_persona,mail_persona,fecha_nacimiento)
	VALUES( @dni_persona,@nombre_persona,@apellido_persona,@direccion_persona,@telefono_persona,@mail_persona,@fecha_nacimiento)

	SET @ID_persona = SCOPE_IDENTITY()

END;
GO

CREATE PROCEDURE DBAS.altaCliente(@dni_persona bigint ,
	@nombre_persona varchar(100),
	@apellido_persona varchar(100),
	@direccion_persona varchar(100),
	@telefono_persona varchar(50),
	@mail_persona varchar(100),
	@fecha_nacimiento datetime)
AS
BEGIN
	DECLARE @ID_persona bigint

	EXECUTE DBAS.altaPersona @dni_persona,@nombre_persona,@apellido_persona,@direccion_persona,	@telefono_persona,@mail_persona,@fecha_nacimiento, @ID_persona OUT

	INSERT INTO DBAS.clientes(id_persona)
	VALUES (@ID_persona)

END;
GO

/* -- ABM Aeronave -- */

CREATE PROCEDURE DBAS.altaAeronave(@ID_fabricante bigint, @ID_servicio bigint, @matricula_aeronave varchar(100), @modelo varchar(100), @KG_disponible float(24), @butacas_pasillo bigint, @butacas_ventana bigint, @piso tinyint)
AS
BEGIN
	DECLARE @mensaje_error varchar(100)
	DECLARE @nro_butaca bigint
	
	SET @nro_butaca = 0
	
	IF EXISTS(SELECT * FROM DBAS.aeronaves WHERE matricula_aeronave = @matricula_aeronave)
	BEGIN
		SET @mensaje_error = 'La matricula ingresada ya existe'
		RAISERROR(@mensaje_error, 12, 1)
	END
	ELSE
	BEGIN
		INSERT INTO DBAS.aeronaves(matricula_aeronave, modelo_aeronave, kg_disponible_encomienda, id_fabricante, id_servicio)
		VALUES(@matricula_aeronave, @modelo, @KG_disponible, @ID_fabricante, @ID_servicio)

		WHILE(@piso != 0)
		BEGIN

			WHILE(@butacas_pasillo != 0)
			BEGIN
				SET @nro_butaca += 1

				INSERT INTO DBAS.butacas(matricula_aeronave, numero_butaca, piso_butaca, tipo_butaca)
				VALUES(@matricula_aeronave, @nro_butaca, @piso, 'Pasillo')

				SET @butacas_pasillo -= 1
			END

			WHILE(@butacas_ventana != 0)
			BEGIN
				SET @nro_butaca += 1

				INSERT INTO DBAS.butacas(matricula_aeronave, numero_butaca, piso_butaca, tipo_butaca)
				VALUES(@matricula_aeronave, @nro_butaca, @piso, 'Ventanilla')

				SET @butacas_ventana -= 1
			END
		
			SET @piso -= 1
		
		END

	END
END;
GO


CREATE PROCEDURE DBAS.cancelacionPasaje(@motivo_cancelacion varchar(100), @ID_cancelacion bigint OUT)
AS
BEGIN
	INSERT INTO DBAS.cancelaciones(fecha_devolucion, motivo_cancelacion)
	VALUES(GETDATE(), @motivo_cancelacion)

	SET @ID_cancelacion = SCOPE_IDENTITY()

END;
GO

CREATE PROCEDURE DBAS.actualizarBajaAeronave(@fecha datetime, @matricula_aeronave varchar(100), @tipo_cancelacion tinyint)
AS
BEGIN
	IF(@tipo_cancelacion = 1)
	BEGIN
		UPDATE DBAS.aeronaves SET fecha_reinsercion = @fecha WHERE matricula_aeronave = @matricula_aeronave
	END
	ELSE
	BEGIN
		UPDATE DBAS.aeronaves SET fecha_baja_servicio_definitiva = @fecha WHERE matricula_aeronave = @matricula_aeronave
	END
END;
GO

CREATE FUNCTION DBAS.estaEnViajeAeronave(@matricula_aeronave varchar(100)) 
RETURNS tinyint
AS
BEGIN
	IF EXISTS(SELECT * FROM DBAS.viajes WHERE matricula_aeronave = @matricula_aeronave AND fecha_salida < GETDATE() AND fecha_llegada IS NULL)
	BEGIN
		RETURN 0
	END
	
	RETURN 1
END;
GO

CREATE FUNCTION DBAS.ciudadAeronave(@matricula_aeronave varchar(100)) 
RETURNS bigint
AS
BEGIN

	RETURN (SELECT TOP 1 tablaCiudades.id_ciudad FROM DBAS.viajes tablaViajes, DBAS.rutas tablaRutas, DBAS.ciudades tablaCiudades 
	WHERE tablaViajes.matricula_aeronave = @matricula_aeronave AND tablaViajes.fecha_llegada <= GETDATE() 
	AND tablaViajes.codigo_ruta = tablaRutas.codigo_ruta AND tablaRutas.ciudad_destino_id = tablaCiudades.id_ciudad
	ORDER BY fecha_llegada DESC)
	
END;
GO

CREATE TABLE DBAS.#aeronavesConUltimoVueloConfirmado (matricula_aeronave varchar(100),id_viaje bigint)
GO


CREATE FUNCTION DBAS.ultimaLlegadaConfirmadaAeronave(@matricula_aeronave varchar(100)) 
RETURNS bigint
AS
BEGIN

	RETURN (SELECT TOP 1 id_viaje FROM DBAS.viajes WHERE matricula_aeronave = @matricula_aeronave AND fecha_llegada IS NULL ORDER BY fecha_salida DESC)
	
END;
GO


CREATE PROCEDURE DBAS.aeronavesDisponiblesParaReemplazo
AS
BEGIN

	DECLARE @matriculaAer varchar (100)
	DECLARE @id_Viaje bigint

	DECLARE aeronaves CURSOR FOR
	SELECT matricula_aeronave FROM DBAS.aeronaves

	OPEN aeronaves

	FETCH NEXT FROM aeronaves
	INTO @matriculaAer

	WHILE @@FETCH_STATUS = 0
	BEGIN
		IF EXISTS(SELECT * FROM DBAS.viajes WHERE matricula_aeronave = @matriculaAer)
		BEGIN
			SET @id_Viaje = DBAS.ultimaLlegadaConfirmadaAeronave(@matriculaAer)
			IF(@id_Viaje IS NOT NULL)
			BEGIN
				INSERT INTO #aeronavesConUltimoVueloConfirmado VALUES (@matriculaAer,@id_Viaje)
			END
		END
		ELSE
		BEGIN
			INSERT INTO #aeronavesConUltimoVueloConfirmado VALUES (@matriculaAer,NULL)
		END
		
		FETCH NEXT FROM aeronaves
		INTO @matriculaAer
	END

	CLOSE aeronaves
	DEALLOCATE aeronaves

END;
GO

CREATE FUNCTION DBAS.aeronavesDisponiblesSinVuelo(@matricula_aeronave varchar(100))
RETURNS @avionesSinVuelo TABLE (matricula_aeronave varchar(100))
AS 
BEGIN
	DECLARE @ciudad_actual_id bigint
	
	SET @ciudad_actual_id = DBAS.ciudadAeronave(@matricula_aeronave)
	
	INSERT INTO  @avionesSinVuelo
	SELECT DISTINCT tablaViajes.matricula_aeronave FROM DBAS.viajes tablaViajes,#aeronavesConUltimoVueloConfirmado tablaAeronavesTemp, DBAS.rutas tablaRutas, DBAS.ciudades tablaCiudades
	WHERE (tablaAeronavesTemp.id_viaje = tablaViajes.id_viaje OR tablaAeronavesTemp.id_viaje IS NULL) AND tablaViajes.fecha_llegada <= GETDATE() 
	AND tablaViajes.codigo_ruta = tablaRutas.codigo_ruta AND tablaRutas.ciudad_destino_id = @ciudad_actual_id

	RETURN	
		
END;
GO

CREATE FUNCTION DBAS.cantidadButacas(@matricula_aeronave varchar(100))
RETURNS INTEGER
AS
BEGIN

	RETURN (SELECT COUNT(id_butaca) FROM DBAS.butacas WHERE matricula_aeronave = @matricula_aeronave AND piso_butaca != 0)

END;
GO

CREATE TRIGGER DBAS.reemplazarButacasEnViajes ON DBAS.viajes
AFTER UPDATE
AS
BEGIN
		DECLARE @id_viaje bigint
		DECLARE @matricula_aeronave_nueva varchar(100)
		DECLARE @id_pasaje bigint
		DECLARE @id_butaca bigint
	
		DECLARE viajesCursor CURSOR FOR
		SELECT id_viaje, matricula_aeronave FROM inserted

		OPEN viajesCursor

		FETCH NEXT FROM viajesCursor
		INTO @id_viaje, @matricula_aeronave_nueva

		WHILE @@FETCH_STATUS = 0
		BEGIN
			UPDATE DBAS.viajes SET matricula_aeronave = @matricula_aeronave_nueva WHERE id_viaje = @id_viaje

			DECLARE pasajesCursor CURSOR FOR
			SELECT @id_pasaje FROM  DBAS.pasajes
			WHERE  DBAS.pasajes.id_viaje = @id_viaje

			DECLARE butacaCursor CURSOR FOR
			SELECT @id_butaca FROM  DBAS.butacas
			WHERE  matricula_aeronave = @matricula_aeronave_nueva
			
			OPEN pasajesCursor
			OPEN butacaCursor

			FETCH NEXT FROM pasajesCursor
			INTO @id_pasaje

			FETCH NEXT FROM butacaCursor
			INTO @id_butaca

			WHILE @@FETCH_STATUS = 0
			BEGIN

				UPDATE DBAS.pasajes SET id_butaca = @id_butaca WHERE id_viaje = @id_viaje

				FETCH NEXT FROM butacaCursor
				INTO @id_butaca

				FETCH NEXT FROM pasajesCursor
				INTO @id_pasaje
			END

			CLOSE pasajesCursor
			DEALLOCATE pasajesCursor

			CLOSE butacaCursor
			DEALLOCATE butacaCursor
			
			FETCH NEXT FROM viajesCursor
			INTO @id_viaje, @matricula_aeronave_nueva
		
		END

		CLOSE viajesCursor
		DEALLOCATE viajesCursor
END;
GO


CREATE PROCEDURE DBAS.cancelacionPasajesBajaAeronave(@fecha datetime, @motivo_cancelacion varchar(100), @matricula_aeronave varchar(100), @tipo_cancelacion tinyint)
AS
BEGIN
	DECLARE @ID_cancelacion bigint

	EXECUTE DBAS.cancelacionPasaje @motivo_cancelacion, @ID_cancelacion OUT

	IF(@tipo_cancelacion = 1)
	BEGIN
		UPDATE tablaPasajes SET tablaPasajes.id_cancelacion = @ID_cancelacion 
		FROM  DBAS.viajes tablaViajes, DBAS.pasajes tablaPasajes
		WHERE tablaViajes.fecha_salida > GETDATE() AND tablaViajes.fecha_salida < @fecha 
		AND tablaPasajes.id_viaje = tablaViajes.id_viaje
		AND tablaViajes.matricula_aeronave = @matricula_aeronave
	
		UPDATE tablaEncomiendas SET tablaEncomiendas.id_cancelacion = @ID_cancelacion 
		FROM  DBAS.viajes tablaViajes, DBAS.encomiendas tablaEncomiendas
		WHERE  tablaViajes.fecha_salida > GETDATE() AND tablaViajes.fecha_salida < @fecha 
		AND tablaEncomiendas.id_viaje = tablaViajes.id_viaje
		AND tablaViajes.matricula_aeronave = @matricula_aeronave

		EXECUTE DBAS.actualizarBajaAeronave @fecha, @matricula_aeronave, @tipo_cancelacion
	
	END
	ELSE
	BEGIN

		UPDATE tablaPasajes SET tablaPasajes.id_cancelacion = @ID_cancelacion 
		FROM  DBAS.viajes tablaViajes, DBAS.pasajes tablaPasajes
		WHERE  tablaViajes.fecha_salida > GETDATE()
		AND tablaPasajes.id_viaje = tablaViajes.id_viaje
		AND tablaViajes.matricula_aeronave = @matricula_aeronave
		
		UPDATE tablaEncomiendas SET tablaEncomiendas.id_cancelacion = @ID_cancelacion 
		FROM  DBAS.viajes tablaViajes, DBAS.encomiendas tablaEncomiendas
		WHERE tablaViajes.fecha_salida > GETDATE()
		AND tablaEncomiendas.id_viaje = tablaViajes.id_viaje
		AND tablaViajes.matricula_aeronave = @matricula_aeronave

		EXECUTE DBAS.actualizarBajaAeronave @fecha, @matricula_aeronave, @tipo_cancelacion
		
	END
END;
GO

CREATE PROCEDURE DBAS.reemplazarAeronave(@matricula_aeronave_original varchar(100),@matricula_aeronave_nueva varchar(100),@baja_temporal tinyint, @fecha datetime)
AS 
BEGIN
	IF (@baja_temporal = 0)
	BEGIN

		UPDATE DBAS.viajes SET matricula_aeronave = @matricula_aeronave_nueva 
		WHERE fecha_salida > GETDATE() AND matricula_aeronave = @matricula_aeronave_original

	END
	ELSE
	BEGIN

		UPDATE DBAS.viajes SET matricula_aeronave = @matricula_aeronave_nueva 
		WHERE fecha_salida > GETDATE() AND fecha_salida < @fecha AND matricula_aeronave = @matricula_aeronave_original
				
	END
END;
GO


CREATE FUNCTION DBAS.aeronavesDeReemplazo(@matricula_aeronave varchar(100))
RETURNS @avionesSinVuelo TABLE (matricula_aeronave varchar(100), cantidad_butacas bigint)
AS 
BEGIN
	
	DECLARE @ciudad_actual_id bigint

	EXECUTE DBAS.aeronavesDisponiblesParaReemplazo

	INSERT INTO @avionesSinVuelo
	SELECT tablaAeronaves1.matricula_aeronave, DBAS.cantidadButacas(tablaAeronaves1.matricula_aeronave) FROM  DBAS.aeronavesDisponiblesSinVuelo(@matricula_aeronave) avionesDisponibles, DBAS.aeronaves tablaAeronaves1, DBAS.aeronaves tablaAeronaves2
	WHERE DBAS.cantidadButacas(avionesDisponibles.matricula_aeronave) >= DBAS.cantidadButacas(@matricula_aeronave) AND tablaAeronaves1.matricula_aeronave = avionesDisponibles.matricula_aeronave AND tablaAeronaves2.matricula_aeronave = @matricula_aeronave
	AND tablaAeronaves1.id_servicio = tablaAeronaves2.id_servicio AND tablaAeronaves1.id_fabricante = tablaAeronaves2.id_fabricante
	
	RETURN
END;
GO

CREATE PROCEDURE DBAS.bajaAeronave(@fecha datetime, @matricula_aeronave varchar(100), @tipo_cancelacion tinyint)
AS
BEGIN
	DECLARE @mensaje_error varchar(100)
	DECLARE @ciudad_actual_id bigint
	IF EXISTS(SELECT * FROM DBAS.viajes WHERE matricula_aeronave = @matricula_aeronave)
	BEGIN

		IF (DBAS.estaEnViajeAeronave(@matricula_aeronave) = 0)
		BEGIN
			SET @mensaje_error = 'No puede dar de baja la aeronave, esta en vuelo'
			RAISERROR(@mensaje_error, 12, 1)
		
		END
		ELSE
		BEGIN
			SET @mensaje_error = 'La aeronave esta en uso'
			RAISERROR(@mensaje_error, 12, 1)
		END
	END
	ELSE
	BEGIN
		EXECUTE DBAS.actualizarBajaAeronave @fecha, @matricula_aeronave, @tipo_cancelacion
	END
END;
GO


